apply plugin: 'maven-publish'
apply plugin: 'signing'


//load properties
File localPropFile = project.rootProject.file('local.properties')

if (localPropFile.exists()) {
    println "Found secret props file, loading props"
    Properties p = new Properties()
    p.load(new FileInputStream(localPropFile))
    p.each { name, value ->
        ext[name] = value
    }
} else {
    println "No props file, loading env vars"
    ext["signing.keyId"] = System.getenv('SIGNING_KEY_ID')
    ext["signing.password"] = System.getenv('SIGNING_PASSWORD')
    ext["signing.secretKeyRingFile"] = System.getenv('SIGNING_SECRET_KEY_RING_FILE')
    ext["ossrhUsername"] = System.getenv('OSSRH_USERNAME')
    ext["ossrhPassword"] = System.getenv('OSSRH_PASSWORD')
}

group = GROUP
version = VERSION_NAME

//java
if (project.plugins.hasPlugin('java-library')) {
    apply plugin: 'java-library'

    java {
        withJavadocJar()
        withSourcesJar()
    }

    javadoc {
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }
}

//android
if (project.plugins.hasPlugin('com.android.library')) {
    task androidSourcesJar(type: Jar) {
        archiveClassifier.set 'sources'
        from android.sourceSets.main.java.source
    }
}

publishing {
    publications {
        release(MavenPublication) {
            artifactId = ARTIFACT_ID
            version = VERSION_NAME

            if (project.plugins.hasPlugin('java-library')) {
                from components.java
            }

            if (project.plugins.hasPlugin('com.android.library')) {
                from components.release
                archive androidSourcesJar
            }

            pom {
                name = ARTIFACT_ID
                description = 'AirRouter是一款轻量级的路由库，区别于主流路由，更轻量级，兼容性更强，使用更简单'
                url = 'https://github.com/myth-china/airrouter-parent'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'yan66665'
                        name = 'yanweiqiang'
                        email = 'yan66665@126.com'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:myth-china/airrouter-parent.git'
                    developerConnection = 'scm:git@github.com:myth-china/airrouter-parent.git'
                    url = 'https://github.com/myth-china/airrouter-parent'
                }
            }
        }
    }

    repositories {
        // The repository to publish to, Sonatype/MavenCentral
        maven {
            // This is an arbitrary name, you may also use "mavencentral" or
            // any other name that's descriptive for you
            name = "sonatype"

            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"

            // You only need this if you want to publish snapshots, otherwise just set the URL
            // to the release repo directly
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            // The username and password we've fetched earlier
            credentials {
                username project.properties.get('ossrhUsername')
                password project.properties.get('ossrhPassword')
            }
        }
    }
}

signing {
    sign publishing.publications
}
